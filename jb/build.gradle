plugins {
    id "java"
    id "idea"
    id "org.jetbrains.intellij" version "0.5.0"
    id "org.jetbrains.kotlin.jvm" version "1.3.50"
}

group "com.codestream"
version "10.0.2"

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    maven { url = "https://europe-maven.pkg.dev/jxbrowser/releases" }
    maven { url = "https://www.jetbrains.com/intellij-repository/snapshots" }
}

ext {
    jxBrowserVersion = "7.7.1"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.50"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.3.0"

//    implementation "com.teamdev.jxbrowser:jxbrowser-cross-platform:${jxBrowserVersion}"
    implementation "com.teamdev.jxbrowser:jxbrowser-win32:${jxBrowserVersion}"
    implementation "com.teamdev.jxbrowser:jxbrowser-win64:${jxBrowserVersion}"
    implementation "com.teamdev.jxbrowser:jxbrowser-mac:${jxBrowserVersion}"
    implementation "com.teamdev.jxbrowser:jxbrowser-linux64:${jxBrowserVersion}"
    implementation "com.teamdev.jxbrowser:jxbrowser-swing:${jxBrowserVersion}"

    implementation group: "org.eclipse.lsp4j", name: "org.eclipse.lsp4j", version: "0.8.0"
    implementation "com.github.salomonbrys.kotson:kotson:2.5.0"
    implementation("io.sentry:sentry:1.7.27") {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    implementation group: 'commons-io', name: 'commons-io', version: '2.6'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

patchPluginXml {
    changeNotes "${file('docs/change-notes.html').getText('UTF-8')}"
    pluginDescription "${file('docs/description.html').getText('UTF-8')}"
}

idea {
    module {
        excludeDirs += file("src/main/resources/agent")
        excludeDirs += file("src/main/resources/webview")
    }
}


intellij {
    type "IC"
    // the version parameter dictates which jars will be used at compilation time
    version "2019.2.2" // earliest version we support
    //  version "201.5616-EAP-CANDIDATE-SNAPSHOT"
    // version "2019.2.3"
    // version "2019.3"
    // version "IC-2019.2.2"
    // version "2018.3.4" // you can also specify dot dot releases
    // version "IC-2018.2.7" //
    // version "PC-2018.2"

    intellij.updateSinceUntilBuild false
    // Run on a locally installed IDE, different from the build dependency specified via type/version
    intellij.alternativeIdePath idePath

    plugins = ['git4idea']
}

runIde {
//    jbrVersion "8u202b1483.58"
//    jbrVersion "11_0_2b159"
//    jvmArgs '-Dsun.java2d.uiScale.enabled=false'
//    jvmArgs '-Dkotlinx.coroutines.debug=on'
//    jvmArgs '-Dcom.codestream.recordRequests=true'
    jvmArgs '-Xmx2048m'
    jvmArgs '-Xms128m'
}

import org.gradle.internal.os.OperatingSystem

task buildDeps(type: Exec) {
    String npm = 'npm';
    if (OperatingSystem.current().isWindows()) {
        npm = 'npm.cmd'
    }

    workingDir "$projectDir"
    commandLine npm, 'run', 'bundle'
}

task buildDepsMac(type: Exec) {
    workingDir "$projectDir"
    commandLine 'npm', 'run', 'bundle:mac'
}

task copyAgent(type: Copy) {
    from "$projectDir/../shared/agent/dist"
    include "**/*"
    into "$projectDir/src/main/resources/agent"
}

task buildDependencies {}
buildDependencies.dependsOn buildDeps, copyAgent

task buildDependenciesMac {}
buildDependenciesMac.dependsOn buildDepsMac, copyAgent

buildPlugin.dependsOn buildDependencies

task buildDebugDeps(type: Exec) {
    String npm = 'npm';
    if (OperatingSystem.current().isWindows()) {
        npm = 'npm.cmd'
    }

    workingDir "$projectDir"
    commandLine npm, 'run', 'build'
}

task copyDebugAgent(type: Copy) {
    from "$projectDir/../shared/agent/dist"
    include "**/*"
    into "$projectDir/src/main/resources/agent"
}
copyDebugAgent.dependsOn buildDebugDeps

task buildDebugDependencies {}
buildDebugDependencies.dependsOn buildDebugDeps, copyDebugAgent

























































